model {
  ################# Observation equation #################
  for (i in 1:n) {
    for (j in 1:4) {
      y[i,j]  ~ dbin(p[j],dx[i,j]) # Noisy observations of transitions
      dx[i,j] ~ dpois(edx[i,j])    # Transitions
    }
  } 

  ################ Evolution equation #####################
  for (i in 2:n) {
    # Expected transitions
    edx[i,1] <- theta[1]*x[i-1,1]*x[i-1,2]/N           # S -> I
    edx[i,2] <- theta[2]*x[i-1,2]                      # I -> R
    edx[i,3] <- theta[3]*x[i-1,1]                      # S -> R, vaccination
    edx[i,4] <- theta[4]*x[i-1,2]                      # I -> D

    # Update states
    #                   S->I    I->R    S->R    I->D
    x[i,1] <- x[i-1,1]-dx[i,1]        -dx[i,3]         # S
    x[i,2] <- x[i-1,2]+dx[i,1]-dx[i,2]        -dx[i,4] # I
    x[i,3] <- x[i-1,3]        +dx[i,2]+dx[i,3]         # R
    x[i,4] <- x[i-1,4]                        +dx[i,4] # D
  }
 
  ############### Evolution for t=1 ###################### 
  # Expected transitions
  edx[1,1] <- theta[1]*X0[1]*X0[2]/N                   # S -> I 
  edx[1,2] <- theta[2]*X0[2]                           # I -> R
  edx[1,3] <- theta[3]*X0[1]                           # S -> R, vaccination
  edx[1,4] <- theta[4]*X0[2]                           # I -> D
 
  # Update states
  #                S->I    I->R    S->R    I->D
  x[1,1] <- X0[1]-dx[1,1]        -dx[1,3]              # S
  x[1,2] <- X0[2]+dx[1,1]-dx[1,2]        -dx[1,4]      # I
  x[1,3] <- X0[3]        +dx[1,2]+dx[1,3]              # R
  x[1,4] <- X0[4]                        +dx[1,4]      # D

 
  ###################### Priors #########################
  for (j in 1:4) {
    theta[j] ~ dgamma(a[j],b[j])
  }
}

